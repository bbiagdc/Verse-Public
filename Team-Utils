    <#
        Takes in a creative object
        and outputs the fort team collection of the playspace gotten from the creative object
    #>
    (CreativeObject:creative_object_interface).GetTeamC()<transacts>:fort_team_collection= CreativeObject.GetPlayspace().GetTeamCollection()
    <#
        Takes in a fort team collection and an int
        and returns the team element of the teams array at the index of the int + 1
    #>
    (FTC:fort_team_collection).GetIndexTeam(TeamIndex:int)<decides><transacts>:team= 
        Teams := FTC.GetTeams()
        Teams[TeamIndex - 1]
    <#
        Takes in an agent, a team index, and a fort team collection
        and sets the agent's team to the team element located at the team index + 1 in the fort team collection's teams array
    #>
    (Agent:agent).ChangeToTeam(TeamIndex:int, FTC:fort_team_collection)<decides><transacts>:void=
        TeamToBe := FTC.GetIndexTeam[TeamIndex]
        FTC.AddToTeam[Agent, TeamToBe]
    <#
        Takes in and agent and a fort team collection
        then returns the team index of the agent's current team
    #>
    (Agent:agent).GetTeamIndex(FTC:fort_team_collection)<decides><transacts>:int=
        CurrentTeam := FTC.GetTeam[Agent]
        FTC.GetIndexOfTeam(CurrentTeam)
    <#
        takes in a fort team collection and team
        and returns that team's index in the fort team collections teams array + 1 if the team is in the array, otherwise returns - 1
    
    ToDO: test this with 'Find' instead of going through the whole array
    #>
    (FTC:fort_team_collection).GetIndexOfTeam(Team:team)<transacts>:int=
        Teams := FTC.GetTeams()
        for(Index := 0..Teams.Length - 1, IndexTeam := Teams[Index], IndexTeam = Team). return Index + 1
        -1
    <#
        Takes in and agent, a fort team collection, and a team index
        then returns true if the agent's team index is the same as the input team index
    #>
    (Agent:agent).IsOnTeam(FTC:fort_team_collection, Index : int)<decides><transacts>:void=
        AgentTeamIndex := Agent.GetTeamIndex[FTC]
        AgentTeamIndex = Index
